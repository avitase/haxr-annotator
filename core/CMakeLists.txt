cmake_minimum_required(VERSION 3.14)

project(core-superbuild)

include(ExternalProject)

option(CORE_BUILD_WASM "Build Emscripten JS/WASM wrapper" ON)

ExternalProject_Add(
    zlib
    SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR}/3rd-party/zlib
    INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/install
    CMAKE_ARGS
        -DBUILD_SHARED_LIBS=OFF
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
        -DCMAKE_PREFIX_PATH:PATH=<INSTALL_DIR>
        -DCMAKE_TOOLCHAIN_FILE:PATH=${CMAKE_TOOLCHAIN_FILE}
        -DZLIB_BUILD_SHARED=OFF
        -DZLIB_BUILD_STATIC=ON
)

ExternalProject_Add(
    hdf5
    DEPENDS zlib 
    SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR}/3rd-party/hdf5
    INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/install
    CMAKE_ARGS
        -DBUILD_SHARED_LIBS=OFF
        -DBUILD_STATIC_LIBS=ON
        -DBUILD_TESTING=OFF
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_FIND_ROOT_PATH:PATH=<INSTALL_DIR>
        -DCMAKE_FIND_ROOT_PATH_MODE_PACKAGE=ONLY
        -DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=ONLY
        -DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=ONLY
        -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
        -DCMAKE_PREFIX_PATH:PATH=<INSTALL_DIR>
        -DCMAKE_TOOLCHAIN_FILE:PATH=${CMAKE_TOOLCHAIN_FILE}
        -DHDF5_BUILD_EXAMPLES=OFF
        -DHDF5_BUILD_CPP_LIB=OFF
        -DHDF5_BUILD_FORTRAN=OFF
        -DHDF5_BUILD_HL_LIB=OFF
        -DHDF5_BUILD_TOOLS=OFF
        -DHDF5_ENABLE_PARALLEL=OFF
        -DHDF5_ENABLE_THREADSAFE=OFF
        -DHDF5_ENABLE_SZIP_ENCODING=OFF
        -DHDF5_ENABLE_SZIP_SUPPORT=OFF
        -DHDF5_ENABLE_ZLIB_SUPPORT=ON
        -DHDF5_USE_ZLIB_STATIC=ON
)

ExternalProject_Add(
    core
    DEPENDS hdf5 
    SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR}/core
    INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/install
    CMAKE_ARGS
        -DBUILD_SHARED_LIBS=OFF
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_FIND_ROOT_PATH:PATH=<INSTALL_DIR>
        -DCMAKE_FIND_ROOT_PATH_MODE_PACKAGE=ONLY
        -DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=ONLY
        -DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=ONLY
        -DCMAKE_PREFIX_PATH:PATH=<INSTALL_DIR>
        -DCMAKE_TOOLCHAIN_FILE:PATH=${CMAKE_TOOLCHAIN_FILE}
        -DCORE_BUILD_WASM:BOOL=${CORE_BUILD_WASM}
        -DCORE_WASM_OUTPUT_DIR:PATH=${CMAKE_CURRENT_BINARY_DIR}/wasm
        -DHDF5_USE_STATIC_LIBRARIES=ON
        -DZLIB_USE_STATIC_LIBS=ON
    BUILD_COMMAND
        ${CMAKE_COMMAND} --build <BINARY_DIR>
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            ${CMAKE_CURRENT_BINARY_DIR}/wasm/core.js
            ${CMAKE_CURRENT_LIST_DIR}/../core.js
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            ${CMAKE_CURRENT_BINARY_DIR}/wasm/core.wasm
            ${CMAKE_CURRENT_LIST_DIR}/../core.wasm
    INSTALL_COMMAND ""
)
